.data
NumDiscos: .asciiz "Numero de discos? "
MoveDisco: .asciiz "\nMove o disco "
MoveDisco2: .asciiz " do pino "
MoveDisco3: .asciiz " para o pino "
 
.text
.globl main
main:
    li $v0,  4          # print string
    la $a0,  NumDiscos
    syscall
    li $v0,  5          # le int
    syscall
 
    # parametros
    add $a0, $v0, $zero # move to $a0
    li $a1, 'A'
    li $a2, 'B'
    li $a3, 'C'
 
    jal hanoi           # chama hanoi
 
    li $v0, 10          # exit
    syscall
 
hanoi:
 
    #preenche o monte
    addi $sp, $sp, -20 
    sw   $ra, 0($sp)
    sw   $s0, 4($sp)
    sw   $s1, 8($sp)
    sw   $s2, 12($sp)
    sw   $s3, 16($sp)
 
    add $s0, $a0, $zero
    add $s1, $a1, $zero
    add $s2, $a2, $zero
    add $s3, $a3, $zero
 
    addi $t1, $zero, 1
    beq $s0, $t1, output
 
    recursao1:
 
        addi $a0, $s0, -1
        add $a1, $s1, $zero
        add $a2, $s3, $zero
        add $a3, $s2, $zero
        jal hanoi
 
        j output
 
    recursao2:
 
        addi $a0, $s0, -1
        add $a1, $s3, $zero
        add $a2, $s2, $zero
        add $a3, $s1, $zero
        jal hanoi
 
    sair:
 
        lw   $ra, 0($sp)        # registros do monte
        lw   $s0, 4($sp)
        lw   $s1, 8($sp)
        lw   $s2, 12($sp)
        lw   $s3, 16($sp)
 
        addi $sp, $sp, 20       # ponteiro do monte
 
        jr $ra
 
    output:
 
        li $v0,  4              # print string
        la $a0,  MoveDisco
        syscall
        li $v0,  1              # print int
        add $a0, $s0, $zero
        syscall
        li $v0,  4              # print string
        la $a0,  MoveDisco2
        syscall
        li $v0,  11             # print char
        add $a0, $s1, $zero
        syscall
        li $v0,  4              # print string
        la $a0,  MoveDisco3
        syscall
        li $v0,  11             # print char
        add $a0, $s2, $zero
        syscall
 
        beq $s0, $t1, sair
        j recursao2
